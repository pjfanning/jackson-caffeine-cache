plugins {
    id 'java-library'
    id 'signing'
    id 'maven-publish'
    id("io.github.gradle-nexus.publish-plugin") version "1.1.0"
}

group = 'com.github.pjfanning'
version = '1.2.1-SNAPSHOT'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.13.2.1'
    implementation 'com.github.ben-manes.caffeine:caffeine:2.9.3'
    testImplementation 'junit:junit:4.13.2'
}

java {
    withJavadocJar()
    withSourcesJar()
}

nexusPublishing {
    repositories {
        sonatype()
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from(components.java)
            pom {
                name = 'jackson-caffeine-cache'
                description = 'A Caffeine based type cache for Jackson.'
                url = 'https://github.com/pjfanning/jackson-caffeine-cache'
                inceptionYear = '2019'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'pjfanning'
                        name = 'PJ Fanning'
                    }
                }
                scm {
                    url = 'https://github.com/pjfanning/jackson-caffeine-cache'
                    connection = 'scm:git://github.com/pjfanning/jackson-caffeine-cache.git'
                    developerConnection = 'scm:git://github.com/pjfanning/jackson-caffeine-cache.git'
                }
            }
        }
    }
}

signing {
    def signingKey = findProperty("signingKey")
    def signingPassword = findProperty("signingPassword")
    useInMemoryPgpKeys(signingKey, signingPassword)
    sign publishing.publications.mavenJava
}
